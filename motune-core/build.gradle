group = 'com.momentum4999.motune'
version = '0.2.0'
description = 'motune-core'

dependencies {
    implementation 'org.ini4j:ini4j:0.5.4'

    implementation "edu.wpi.first.wpilibj:wpilibj-java:${wpilibversion}"
    implementation "edu.wpi.first.ntcore:ntcore-java:${wpilibversion}"

    // Align versions of all Kotlin components
    implementation platform('org.jetbrains.kotlin:kotlin-bom')

    // Use the Kotlin JDK 8 standard library.
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

    // Use the Kotlin test library.
    testImplementation 'org.jetbrains.kotlin:kotlin-test'

    // Use the Kotlin JUnit integration.
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
}

tasks.register('fatJar', Jar) {
    // We build a "fat jar" that contains all the dependencies (ini4j, kotlin stdlib, etc.),
    // so that we don't need to also include the dependencies every time we use the library in robot code

    archiveAppendix = 'fatJar'

    println("Compiling fatJar for " + project.description)
    def dependencies = configurations.compileClasspath.filter{
        def name = it.name
        excludeFromFatJar.every {!name.startsWith(it) }
    }.each{ println(it) }.collect{ it.isDirectory() ? it : zipTree(it) }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { dependencies }
    with jar
}
